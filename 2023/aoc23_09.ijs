data =: 0 : 0
0 3 6 9 12 15
1 3 6 10 15 21
10 13 16 21 30 45
)

data =: freads 'aoc23_09.txt'

parsed =: (< @ (". ;. _1 @ (' '&,))) ;. _2 data

NB. Принимает на вход список из трех упакованных элементов:
NB. двух аккумуляторов (для левых и правых разностей) и
NB. текущего набора значений. Возвращает следующую итерацию цикла.

NextDiff =: (> @ (0&{),({. @ > @ {:)) ; (>@ (1&{) ,({: @ > @ {:)) ; ((}.-}:) @ > @ {:) 

NB. Проверяет, что все значения в упакованном векторе являются нулями.

Zeroes =: *./ @ (0&=) @ >

NB. Выполняет итерации цикла, пока последний элемент не будет
NB. создержать только нули.

Converge =: }: @ (NextDiff ^: (-. @ Zeroes @ {:) ^: _) @ ((0$0)&;^:2)

Solution =: ((+/@]) (+/@>@{: , -/@(,&0)@>@{.) @ Converge "0)

Solution parsed
