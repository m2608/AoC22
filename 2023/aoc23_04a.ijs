data =: 0 : 0
Card 1: 41 48 83 86 17 | 83 86  6 31 17  9 48 53
Card 2: 13 32 20 16 61 | 61 30 68 82 17 32 24 19
Card 3:  1 21 53 59 44 | 69 82 63 72 16 21 14  1
Card 4: 41 92 73 84 69 | 59 84 76 51 58  5 54 83
Card 5: 87 83 26 28 32 | 88 30 70 12 93 22 82 36
Card 6: 31 18 13 56 72 | 74 77 10 23 35 67 36 11
)

NB. data =: freads 'aoc23_04.txt'

NB. Парсим часть строки с числами и возвращаем два набора чисел.
Numbers      =: ((< @ ".);._1) @ ('|'&,) @ >

NB. Парсим строку карточки и возвращаем два набора чисел.
ParseLine    =: Numbers @ (1&{) @ (<;._1 @ (':'&,))

NB. Считаем количество совпадений в двух наборах чисел.
CountMatches =: +/ @ ((> @ (0&{)) e. (> @ (1&{)))

NB. Получаем количество очков для текущей карточки в соответствии
NB. с количеством совпадений.
GetPoints    =: ((>&0)*(2&^ @ <:)) @ CountMatches @ ParseLine

Solution1    =: +/ @ (GetPoints ;. _2)

Solution1 data
